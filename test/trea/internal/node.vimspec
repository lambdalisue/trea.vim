Describe trea#internal#node
  Before
    let TIMEOUT = 5000
    let Promise = vital#trea#import('Async.Promise')
    let CancellationToken = vital#trea#import('Async.CancellationToken')
    let token = CancellationToken.none
    let provider = trea#scheme#debug#provider#new()
    let Comparator = function('trea#comparator#default#compare')
  End

  Describe #debug()
    It returns a debug information of a given node as string
      let node = trea#internal#node#root('debug:///shallow', provider)
      Assert IsString(trea#internal#node#debug(node))
    End
  End

  Describe #index()
    Before
      let root = trea#internal#node#root('debug:///', provider)
      let [children, _] = Promise.wait(
            \ trea#internal#node#children(root, provider, token),
            \ { 'timeout': TIMEOUT },
            \)
      let nodes = [root] + children
    End

    It returns an index of node which has a given key
      Assert Equals(trea#internal#node#index([], nodes), 0)
      Assert Equals(trea#internal#node#index(['shallow'], nodes), 1)
      Assert Equals(trea#internal#node#index(['deep'], nodes), 2)
      Assert Equals(trea#internal#node#index(['heavy'], nodes), 3)
      Assert Equals(trea#internal#node#index(['leaf'], nodes), 4)
    End

    It returns -1 when no node exists for a given key
      Assert Equals(trea#internal#node#index(['missing'], nodes), -1)
    End
  End

  Describe #find()
    Before
      let root = trea#internal#node#root('debug:///', provider)
      let [children, _] = Promise.wait(
            \ trea#internal#node#children(root, provider, token),
            \ { 'timeout': TIMEOUT },
            \)
      let nodes = [root] + children
    End

    It returns an index of node which has a given key
      Assert Equals(trea#internal#node#find([], nodes), nodes[0])
      Assert Equals(trea#internal#node#find(['shallow'], nodes), nodes[1])
      Assert Equals(trea#internal#node#find(['deep'], nodes), nodes[2])
      Assert Equals(trea#internal#node#find(['heavy'], nodes), nodes[3])
      Assert Equals(trea#internal#node#find(['leaf'], nodes), nodes[4])
    End

    It returns v:null when no node exists for a given key
      Assert Equals(trea#internal#node#find(['missing'], nodes), v:null)
    End
  End

  Describe #root()
    It returns a node instance of a given URL and provider
      let node = trea#internal#node#root('debug:///shallow', provider)
      Assert KeyExists(node, 'name')
      Assert KeyExists(node, 'status')
      Assert KeyExists(node, 'label')
      Assert KeyExists(node, 'hidden')
      Assert KeyExists(node, 'bufname')
      Assert KeyExists(node, '__key')
      Assert KeyExists(node, '__owner')
      Assert KeyExists(node, 'processing')
      Assert KeyExists(node, '__cache')
      Assert KeyExists(node, '__promise')
    End
  End

  Describe #parent()
    Before
      let node = trea#internal#node#root('debug:///shallow/alpha', provider)
    End

    It returns a promise
      let p = trea#internal#node#parent(node, provider, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a parent node of a given node
      let [r, e] = Promise.wait(
            \ trea#internal#node#parent(node, provider, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(r._uri, '/shallow')

      let [r, e] = Promise.wait(
            \ trea#internal#node#parent(r, provider, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(r._uri, '/')
    End
  End

  Describe #children()
    Before
      let node = trea#internal#node#root('debug:///shallow', provider)
    End

    It returns a promise
      let p = trea#internal#node#children(node, provider, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a list of child nodes of a given node
      let [r, e] = Promise.wait(
            \ trea#internal#node#children(node, provider, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 3)
      Assert Equals(r[0]._uri, '/shallow/alpha')
      Assert Equals(r[1]._uri, '/shallow/beta')
      Assert Equals(r[2]._uri, '/shallow/gamma')
    End
  End

  Describe #expand()
    Before
      let root = trea#internal#node#root('debug:///', provider)
    End

    It returns a promise
      let p = trea#internal#node#expand(root, [root], provider, Comparator, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a list of nodes
      let [r, e] = Promise.wait(
            \ trea#internal#node#expand(root, [root], provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 5)
      Assert Equals(r[0]._uri, '/')
      Assert Equals(r[1]._uri, '/deep')
      Assert Equals(r[2]._uri, '/heavy')
      Assert Equals(r[3]._uri, '/shallow')
      Assert Equals(r[4]._uri, '/leaf')

      let node = r[3]
      let [r, e] = Promise.wait(
            \ trea#internal#node#expand(node, r, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 8)
      Assert Equals(r[0]._uri, '/')
      Assert Equals(r[1]._uri, '/deep')
      Assert Equals(r[2]._uri, '/heavy')
      Assert Equals(r[3]._uri, '/shallow')
      Assert Equals(r[4]._uri, '/shallow/alpha')
      Assert Equals(r[5]._uri, '/shallow/beta')
      Assert Equals(r[6]._uri, '/shallow/gamma')
      Assert Equals(r[7]._uri, '/leaf')
    End
  End

  Describe #collapse()
    Before
      let root = trea#internal#node#root('debug:///', provider)
      let [nodes, e] = Promise.wait(
            \ trea#internal#node#expand(root, [root], provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      let node = nodes[3]
      let [nodes, e] = Promise.wait(
            \ trea#internal#node#expand(node, nodes, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
    End

    It returns a promise
      let p = trea#internal#node#collapse(node, nodes, provider, Comparator, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a list of nodes
      let [r, e] = Promise.wait(
            \ trea#internal#node#collapse(node, nodes, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 5)
      Assert Equals(r[0]._uri, '/')
      Assert Equals(r[1]._uri, '/deep')
      Assert Equals(r[2]._uri, '/heavy')
      Assert Equals(r[3]._uri, '/shallow')
      Assert Equals(r[4]._uri, '/leaf')
    End
  End

  Describe #reload()
    Before
      let root = trea#internal#node#root('debug:///', provider)
      let [nodes, e] = Promise.wait(
            \ trea#internal#node#expand(root, [root], provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      let node = nodes[3]
      let [nodes, e] = Promise.wait(
            \ trea#internal#node#expand(node, nodes, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
    End

    It returns a promise
      let p = trea#internal#node#reload(node, nodes, provider, Comparator, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a list of nodes
      let [r, e] = Promise.wait(
            \ trea#internal#node#reload(node, nodes, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 8)
      Assert Equals(r[0]._uri, '/')
      Assert Equals(r[1]._uri, '/deep')
      Assert Equals(r[2]._uri, '/heavy')
      Assert Equals(r[3]._uri, '/shallow')
      Assert Equals(r[4]._uri, '/shallow/alpha')
      Assert Equals(r[5]._uri, '/shallow/beta')
      Assert Equals(r[6]._uri, '/shallow/gamma')
      Assert Equals(r[7]._uri, '/leaf')
    End
  End

  Describe #reveal()
    Before
      let root = trea#internal#node#root('debug:///', provider)
      let [nodes, e] = Promise.wait(
            \ trea#internal#node#expand(root, [root], provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
    End

    It returns a promise
      let p = trea#internal#node#reveal([], nodes, provider, Comparator, token)
      Assert True(Promise.is_promise(p))
    End

    It resolves to a list of nodes
      let [r, e] = Promise.wait(
            \ trea#internal#node#reveal(['deep', 'alpha', 'beta', 'gamma'], nodes, provider, Comparator, token),
            \ { 'timeout': TIMEOUT },
            \)
      Assert Equals(e, v:null)
      Assert Equals(len(r), 8)
      Assert Equals(r[0]._uri, '/')
      Assert Equals(r[1]._uri, '/deep')
      Assert Equals(r[2]._uri, '/deep/alpha')
      Assert Equals(r[3]._uri, '/deep/alpha/beta')
      Assert Equals(r[4]._uri, '/deep/alpha/beta/gamma')
      Assert Equals(r[5]._uri, '/heavy')
      Assert Equals(r[6]._uri, '/shallow')
      Assert Equals(r[7]._uri, '/leaf')
    End
  End
End
